/*******************************************************************************
 * Copyright (c) 2014-2016 IncQuery Labs Ltd.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     Akos Horvath, Abel Hegedus, Tamas Borbas, Marton Bur, Zoltan Ujhelyi, Robert Doczi - initial API and implementation
 *******************************************************************************/
package org.eclipse.viatra.examples.cps.xform.m2m.batch.eiq.queries

import "http://org.eclipse.viatra/model/cps"
import "http://org.eclipse.viatra/model/deployment"
import java org.eclipse.viatra.examples.cps.xform.m2m.util.SignalUtil

pattern hostInstances(hostType : HostType, hostInstance : HostInstance) {
	HostType.instances(hostType, hostInstance);
}

pattern depTransition(depTransition : BehaviorTransition) {
	BehaviorTransition(depTransition);
}

pattern stateMachineTransitions(cpsBeh : StateMachine, trans : Transition) {
	StateMachine.states(cpsBeh, state);
	State.outgoingTransitions(state, trans);
}

pattern applicationInstance(appType : ApplicationType, appInstance : ApplicationInstance, host : HostInstance) {
	ApplicationType.instances(appType, appInstance);
	ApplicationInstance.allocatedTo(appInstance, host);
}

pattern transitionAction(transition : Transition, appType : ApplicationType, action : java String) {
	State.outgoingTransitions(state, transition);
	StateMachine.states(sm, state);
	ApplicationType.behavior(appType, sm);
	Transition.action(transition, action);
}

pattern cpsApplicationTransition(cpsApp : ApplicationInstance, cpsTransition : Transition) {
	ApplicationType.instances(cpsAppType, cpsApp);
	ApplicationType.behavior.states.outgoingTransitions(cpsAppType, cpsTransition);
}

pattern sendTransitionAppSignal(transition : Transition, app : java String, signal : java String) {
 	Transition.action(transition, action);
 	check(SignalUtil.isSend(action));
 	app == eval(SignalUtil.getAppId(action));
 	signal == eval(SignalUtil.getSignalId(action));
}

pattern waitTransitionAppSignal(transition : Transition, appType : ApplicationType, signal : java String) {
 	ApplicationType.behavior(appType, stateMachine);
	StateMachine.states(stateMachine, source);
	State.outgoingTransitions(source, transition);
 	Transition.action(transition, action);
 	check(SignalUtil.isWait(action));
 	signal == eval(SignalUtil.getSignalId(action));
}

pattern triggerPair(cpsTrigger : Transition, cpsTarget : Transition) {
	find sendTransitionAppSignal(cpsTrigger, appId, signal);
	find waitTransitionAppSignal(cpsTarget, waitApp, signal);
	ApplicationType.identifier(waitApp, appId);
}

pattern communicatingAppInstances(sourceAppInstance : ApplicationInstance, targetAppInstance : ApplicationInstance) {
	find applicationInstance(_, sourceAppInstance, sourceHostInstance);
	find applicationInstance(_, targetAppInstance, targetHostInstance);
	find reachableHosts(sourceHostInstance, targetHostInstance);
}

pattern hostCommunication(fromHost : HostInstance, toHost : HostInstance) {
	HostInstance.communicateWith(fromHost, toHost);
}

pattern reachableHosts(source : HostInstance, target : HostInstance) {
	find hostCommunication(source, target);
} or {
	HostInstance(source);
	HostInstance(target);
	source == target;
}